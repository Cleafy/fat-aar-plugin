apply plugin: 'com.android.library'
apply plugin: 'me.vigi.fat-aar'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 25
        versionCode 1
        versionName '1.0'
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    // aar project
    embed project(':aar-lib')
    // jar project
    embed project(':java-lib')

    // aar, minsdk=9, has res, has proguard.txt, has public.txt, has R.txt
    // has dependencies(support-annotations, support-compat, support-core-ui)
//    embed 'com.android.support:recyclerview-v7:25.1.1'
    // jar, no compile dependency
    embed 'com.google.guava:guava:20.0'
    // aar, minsdk=8, no dependency
    embed 'com.android.volley:volley:1.0.0'
    // aar, minsdk=9, has dependency(io.reactivex:rxjava:1.1.6)
    embed 'io.reactivex:rxandroid:1.2.1'
    // jar, no dependency
    embed 'io.reactivex:rxjava:1.2.5'

    // jar file
    compile files('libs/commons-lang-2.6.jar')

    compile 'com.squareup.okhttp3:okhttp:3.6.0'
    compile 'com.squareup.retrofit2:retrofit:2.1.0'
}

apply plugin: 'maven'
uploadArchives {
    repositories {
        mavenDeployer {
            pom.groupId = 'me.vigi.fat-aar'
            pom.artifactId = 'demo.lib'
            pom.version = '1.0'
            repository(url: uri('../../repo'))
        }
    }
}

project.gradle.addListener(new TaskExecutionListener() {
            @Override
            void beforeExecute(Task task) {}

            @Override
            void afterExecute(Task task, TaskState state) {
                task.inputs.files.each {
                    println '    input=' + it
                }
                task.outputs.files.each {
                    println '    output=' + it
                }
            }
        })